apply plugin: 'kotlin2js'
apply plugin: 'org.jetbrains.kotlin.frontend'

// Skip tasks
classes.enabled = false
jar.enabled = false

dependencies {
    compile project(':guide:index')
    compile project(':guide:instance')
    compile project(':guide:syntax')
    compile project(':guide:computed')
    compile project(':guide:class-and-style')
    compile project(':guide:list')
    compile project(':guide:events_')
    compile project(':guide:forms')
    compile project(':guide:components')
    compile project(':guide:filters')
}

compileKotlin2Js {
    kotlinOptions {
        sourceMap = true
    }
}

compileTestKotlin2Js {
    kotlinOptions {
        sourceMap = true
    }
}

kotlinFrontend {
    downloadNodeJsVersion = node_version
    sourceMaps = true

    npm {
        dependency('vue', vue_version)
        dependency('axios', '0.12.0')

        devDependency('webpack', '4.29.5')
    }

    webpackBundle {
        publicPath = "/"
        port = 8088
        stats = "errors-only"
    }
}

subprojects {

    apply plugin: 'kotlin2js'

    // Skip tasks
    classes.enabled = false
    jar.enabled = false

    // Kotlin/JS library dependencies
    dependencies {
        compile "org.musyozoku:vuekt:$vuekt_version"
    }

    sourceSets {
        main.kotlin.srcDirs += "main"
        test.kotlin.srcDirs += "test"
    }

    compileKotlin2Js {
        kotlinOptions {
            moduleKind = "commonjs"
            sourceMap = true
        }
    }
}

// Task dependencies
def publishVuektTask = tasks.getByPath(':vuekt:publishToMavenLocal')

packages.dependsOn(publishVuektTask)
packages.mustRunAfter(publishVuektTask)